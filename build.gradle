plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2026.1.1"
    id "com.peterabeles.gversion" version "1.10"
    id "io.freefair.lombok" version "8.4"
    id "com.diffplug.spotless" version "6.12.0"
    id "io.freefair.aspectj.post-compile-weaving" version "8.4"
}

ext {
    deploySrcDir = "src/main/deploy"
    deployRobotDir = "/home/lvuser/deploy"
    useLocalGompeiLib = project.hasProperty("useLocalGompeiLib")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def ROBOT_MAIN_CLASS = "frc.robot.Main"

/* =====================================================
 * Repositories
 * ===================================================== */
repositories {
    // REQUIRED for local SNAPSHOT resolution
    mavenLocal()

    def gprUser = project.findProperty("gpr.user")
    def gprKey = project.findProperty("gpr.key")

    if (!useLocalGompeiLib && gprUser != null && gprKey != null) {
        exclusiveContent {
            forRepository {
                maven {
                    url = uri("https://maven.pkg.github.com/Team-190/GompeiLib")
                    credentials {
                        username = gprUser
                        password = gprKey
                    }
                }
            }
            filter {
                includeGroup "edu.wpi.team190"
            }
        }
    } else if (!useLocalGompeiLib) {
        exclusiveContent {
            forRepository {
                maven { url = uri("https://jitpack.io") }
            }
            filter {
                includeGroup "com.github.Team-190"
            }
        }
    }
}

/* =====================================================
 * Detect RobotType â†’ deploy folder
 * ===================================================== */
gradle.projectsEvaluated {
    def buildDir = file("$buildDir/classes/java/main")
    if (!buildDir.exists()) {
        project.ext.deploySrcDir = "src/main/deploy"
        return
    }

    try {
        def loader = new URLClassLoader([buildDir.toURI().toURL()] as URL[], this.class.classLoader)
        def constantsClass = loader.loadClass("frc.robot.RobotConfig")
        def robotField = constantsClass.getDeclaredField("ROBOT")
        robotField.setAccessible(true)
        def robotEnumValue = robotField.get(null)

        def baseName = robotEnumValue.toString().toLowerCase().replaceAll(/_sim$/, "")
        def candidateDir = "src/main/deploy/$baseName"

        if (file(candidateDir).exists()) {
            project.ext.deploySrcDir = candidateDir
            project.ext.deployRobotDir = "/home/lvuser/deploy/$baseName"
        }
    } catch (Throwable ignored) {
        project.ext.deploySrcDir = "src/main/deploy"
    }
}

/* =====================================================
 * Deploy Configuration
 * ===================================================== */
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                    jvmArgs.addAll([
                        "-XX:+UnlockExperimentalVMOptions",
                        "-XX:GCTimeRatio=5",
                        "-XX:+UseSerialGC",
                        "-XX:MaxGCPauseMillis=50",
                        "-Xmx100M",
                        "-Xms100M",
                        "-XX:+AlwaysPreTouch"
                    ])
                }

                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree(project.ext.deploySrcDir)
                    directory = deployRobotDir
                    deleteOldFiles = false
                }
            }
        }
    }
}

repositories {
    exclusiveContent {
        forRepositories(
                mavenLocal(),
                maven {
                    name = "GompeiLibGitHubPackages"
                    url = uri("https://maven.pkg.github.com/Team-190/GompeiLib")
                    credentials {
                        username = project.findProperty("gpr.user")
                        password = project.findProperty("gpr.key")
                    }
                }
                )
        filter {
            includeGroup "edu.wpi.team190"
        }
    }

    // Normal repos for everything else
    mavenCentral()
    maven { url = uri("https://frcmaven.wpi.edu/artifactory/release") }
    maven { url = uri("https://frcmaven.wpi.edu/artifactory/littletonrobotics-mvn-release") }
    maven { url = uri("https://maven.ctr-electronics.com/release/") }
}


// Grab the roboRIO comment via SSH

import com.jcraft.jsch.JSch
import edu.wpi.first.gradlerio.GradleRIOPlugin
import groovy.json.JsonSlurper

ext {
    // RoboRIO SSH details
    roboRIOHost = 'roboRIO-190-FRC.local' // Replace with your team number
    roboRIOUser = 'admin'                 // Default user
    roboRIOPassword = ''                  // Default password
    machineInfoFile = '/etc/machine-info' // Path to the file to read
}

task checkRoboRIOtoRobotType {
    dependsOn compileJava
    doLast {
        def ROBORIO_COMMENT = fetchNameUsingJSch(
                roboRIOHost, roboRIOUser, roboRIOPassword, machineInfoFile
                )
        println "RoboRIO Name: ${ROBORIO_COMMENT}"

        // Ensure the build output directory is in the classpath
        def buildOutputDir = file('build/classes/java/main')
        URLClassLoader loader = new URLClassLoader([
            buildOutputDir.toURI().toURL()
        ] as URL[], this.class.classLoader)

        // Set file path names and constants
        def robotTypeClassName = 'frc.robot.RobotConfig$RobotType'
        def constantsClassName = 'frc.robot.RobotConfig'
        def robotType = null
        def constantsClass = null
        def robotFieldName = 'ROBOT'

        try {
            // Load the class from the extended classloader
            robotType = loader.loadClass(robotTypeClassName)
            println "Loaded class: ${robotType.name}"

            def constants = robotType.enumConstants
            println "Enum constants: ${constants.collect { it.name() }}"

        } catch (ClassNotFoundException e) {
            println "Error: Class ${robotTypeClassName} not found. ${e.message}"
        }

        // Check for valid name of roboRIO comment
        boolean foundMatch = false
        for (enumConstant in robotType.enumConstants) {
            if (enumConstant.toString().equalsIgnoreCase(ROBORIO_COMMENT)) {
                foundMatch = true
                break // Exit the loop once a match is found
            }
        }

        if (foundMatch) {
            println "Match found for possible RobotType"
        } else {
            throw new GradleException("Error: '${ROBORIO_COMMENT}' is not a valid RobotType\nTo Fix: Enter valid RoboRIO comment of valid RobotType in file '${machineInfoFile}' or via Webserver!")
        }

        try {
            // Load the class from the extended classloader
            constantsClass = loader.loadClass(constantsClassName)
            println "Loaded class: ${constantsClass.name}"

        } catch (ClassNotFoundException e) {
            println "Error: Class ${constantsClassName} not found. ${e.message}"
        }

        // Get the ROBOT field
        def robotField = constantsClass.getDeclaredField(robotFieldName)
        robotField.setAccessible(true)

        // Get the value of the ROBOT field
        def robotEnumValue = robotField.get(null) // For static fields, pass `null`
        println "Declared ROBOT in Constants.java: ${robotEnumValue}"

        // Compare roboRIO name with ROBOT value
        if (ROBORIO_COMMENT.equalsIgnoreCase(robotEnumValue.name())) {
            println "The roboRIO name matches the ROBOT value in Constants.java!"
        } else {
            throw new GradleException("Mismatch! The RoboRIO comment '${ROBORIO_COMMENT}' does not match the ROBOT value '${robotEnumValue.name()}'\nTo Fix: Match RoboRIO comment to ROBOT in file '${constantsClassName}'")
        }

    }
}

// Function to fetch the file content using JSch
def fetchNameUsingJSch(host, user, password, remoteFile) {
    def session = null
    def channel = null
    try {
        JSch jsch = new JSch()
        session = jsch.getSession(user, host, 22)
        session.setPassword(password)

        // Skip strict host key checking for simplicity
        session.setConfig("StrictHostKeyChecking", "no")

        // Connect to the session
        session.connect()

        // Open an exec channel to run the command
        channel = session.openChannel("exec")
        def command = "cat ${remoteFile}"
        channel.setCommand(command)

        // Capture the output
        def inputStream = channel.inputStream
        channel.connect()

        def output = inputStream.text.trim()
        if (output.isEmpty()) {
            throw new GradleException("Failed to fetch data from ${remoteFile}")
        }

        // Extract the name within quotes if the output contains PRETTY_HOSTNAME
        def matcher = output =~ /PRETTY_HOSTNAME="(.+?)"/
        if (matcher.find()) {
            return matcher.group(1) // The name within quotes
        } else {
            throw new GradleException("RoboRIO Comment is Empty in file '${remoteFile}'\nTo Fix: Add RoboRIO Comment in file '${remoteFile}' or via Webserver!")
        }
    } catch (Exception e) {
        throw new GradleException("Error during SSH connection: ${e.message}", e)
    } finally {
        if (channel != null) {
            channel.disconnect()
        }
        if (session != null) {
            session.disconnect()
        }
    }
}

deployroborio.dependsOn(checkRoboRIOtoRobotType)


def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Configuration for AdvantageKit
task(replayWatch, type: JavaExec) {
    mainClass = "org.littletonrobotics.junction.ReplayWatch"
    classpath = sourceSets.main.runtimeClasspath
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    annotationProcessor wpi.java.deps.wpilibAnnotations()
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation 'org.jgrapht:jgrapht-io:1.5.1'


    // implementation 'edu.wpi.team190:gompeilib:2026.+'
    implementation "edu.wpi.team190:gompeilib:0.0.0-SNAPSHOT"
    // This is required for the @Tracer annotations to work at runtime
    implementation 'org.aspectj:aspectjrt:1.9.21'
    implementation 'org.aspectj:aspectjtools:1.9.21'

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    def akitJson = new groovy.json.JsonSlurper()
            .parseText(file("vendordeps/AdvantageKit.json").text)
    annotationProcessor "org.littletonrobotics.akit:akit-autolog:$akitJson.version"
}

test {
    useJUnitPlatform()
}

/* =====================================================
 * Jar + WPILib
 * ===================================================== */
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from sourceSets.main.allSource
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

deploy.targets.roborio.artifacts.frcJava.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

/* =====================================================
 * Compiler / Versioning
 * ===================================================== */
tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
}

compileJava.dependsOn(createVersionFile)

gversion {
    srcDir = "src/main/java/"
    classPackage = "frc.robot"
    className = "BuildConstants"
    dateFormat = "yyyy-MM-dd HH:mm:ss z"
    timeZone = "America/New_York"
}

/* =====================================================
 * Spotless
 * ===================================================== */
compileJava.dependsOn(spotlessApply)
spotless {
    java {
        target fileTree(".") {
            include "**/*.java"
            exclude "**/build/**"
        }
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}
